group 'io.ably.flutter.plugin'
version '1.0'

buildscript {
    ext.protobufVersion = '0.8.8'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "com.google.protobuf:protobuf-gradle-plugin:$protobufVersion"
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

dependencies {
    implementation 'io.ably:ably-android:1.1.10'
}

android {
    compileSdkVersion 28

    sourceSets {
        main{
            proto{
                srcDirs '../protos'
            }
        }
    }

    // Require Java language level 8 so we can use Method References (used with Lambdas)
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    defaultConfig {
        // Require Android 7.0 (Nougat) runtime so we can use Lambda expressions
        minSdkVersion 24

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.6.1'
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite { }
//                java { option "lite" }
            }
        }
    }
}

dependencies {
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
//    implementation 'com.google.protobuf:protobuf-java-util:3.12.0'
}